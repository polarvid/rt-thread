Import('rtconfig')
from building import *

cwd     = GetCurrentDir()
src     = set()
CPPPATH = [cwd]

# for arch specified routines
arch_file = {
    "aarch64": ["aarch64-ctx.S", "aarch64-utils.c"],
    "riscv64": ["riscv64.S"],
}
arch    = rtconfig.ARCH
cpu     = rtconfig.CPU

# fix the cpu for risc-v
if arch == 'risc-v':
    rv64 = ['virt64', 'c906']
    if cpu in rv64:
        arch = 'riscv64'

if GetDepend(['TRACING_FTRACE']):
    # append arch prefix
    for file in arch_file[arch]:
        src.add('arch/' + file)
    src = src.union({'ftrace.c', 'ftrace-symtbl.c', 'fgraph.c', 'ftrace-test.c', 'fgraph-app-test.c'})
if GetDepend(['TRACING_KSYMTBL']):
    src = src.union({'ksymtbl.c'})

# Generic codes
if len(src) > 0:
    generic_code = {'search.c', 'event-ring.c', 'evt-ring-test.c'}
    src = src.union(generic_code)

group = DefineGroup('tracing', list(src), depend = ['RT_USING_TRACING'], CPPPATH = CPPPATH)

Return('group')
