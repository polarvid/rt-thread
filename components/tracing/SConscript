Import('rtconfig')
from building import *

cwd     = GetCurrentDir()
src     = set()
CPPPATH = [cwd]

# for arch specified routines
arch_file = {
    "aarch64": ["aarch64-ctx.S", "aarch64-utils.c"],
    "riscv64": ["riscv64.S"],
}
arch    = rtconfig.ARCH
cpu     = rtconfig.CPU

# fix the cpu for risc-v
if arch == 'risc-v':
    rv64 = ['virt64', 'c906']
    if cpu in rv64:
        arch = 'riscv64'

# append arch prefix
for file in arch_file[arch]:
    src.add('arch/' + file)

# Generic codes
src.add('search.c')

if GetDepend(['TRACING_FTRACE']):
    src = src.union({'ftrace.c', 'ftrace-symtbl.c', 'fgraph.c', 'ftrace-test.c'})
if GetDepend(['TRACING_KSYMTBL']):
    src = src.union({'ksymtbl.c'})

group = DefineGroup('tracing', list(src), depend = ['RT_USING_TRACING'], CPPPATH = CPPPATH)

Return('group')
